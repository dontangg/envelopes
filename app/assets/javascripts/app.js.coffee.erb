# TODO:
# * Handle errors
# * Handle authorization
# * Always use callbacks?
# * Don't change content until loaded - right now we're changing envelopes, then AJAX-ing content in

app = angular.module 'envelopes', ['ngRoute']
  .config ($routeProvider, $locationProvider) ->

    if window.history && window.history.pushState
      $locationProvider.html5Mode true

    $routeProvider
      .when "/envelopes/:envelopeId",
        templateUrl: '<%= asset_path('partials/envelope.html') %>'
        controller: 'EnvelopeCtrl'


app.filter 'envelopeUrl', ->
  (envelope) ->
    name = envelope.name.toLowerCase().replace " ", "-"
    name = name.replace /[^a-zA-Z-]/, ""
    "/envelopes/#{envelope.id}-#{name}"




app.factory 'envelopeService', ($http) ->

  envelopes = null
  getEnvelopes = (callback) ->
    callback envelopes if envelopes

    $http.get('/envelopes.json')
      .success (data) ->
        organized_envelopes = data[""]
        stack = data[""].slice() # Make a copy of the root-level array

        while stack.length
          env = stack.pop()
          if data[env.id]
            stack = stack.concat data[env.id]
            env.children = data[env.id]

        envelopes = organized_envelopes
        callback organized_envelopes
      .error (data) ->
        console.log data

  findById = (id, callback) ->
    unless envelopes?
      getEnvelopes ->
        findById id, callback
      return

    stack = envelopes.slice()
    id = parseInt id
    while stack.length
      env = stack.pop()
      if env.id == id
        callback env
        return

      stack = stack.concat env.children if env.children

    false


  all: getEnvelopes
  find: findById

app.factory 'transactionService', ($http) ->

  find: (options, callback) ->
    envelopeId = options.envelopeId
    options =
      params:
        show_transfers: options.showTransfers

    $http.get("/envelopes/#{envelopeId}/transactions.json", options)
      .success (data) ->
        console.log data
        callback data
      .error (data) ->
        console.log data



app.directive 'leftPanel', ->
  templateUrl: '<%= asset_path('partials/left_panel.html') %>'
  controller: ($scope, envelopeService) ->
    envelopeService.all (envelopes) ->
      $scope.envelopes = envelopes
      console.log envelopes



app.controller 'EnvelopeCtrl', ['$scope', '$routeParams', 'envelopeService', 'transactionService',
  ($scope, $routeParams, envelopeService, transactionService) ->
    envelopeService.find $routeParams.envelopeId, (envelope) ->
      $scope.envelope = envelope

      transactionService.find
        envelopeId: envelope.id
        showTransfers: true
        , (transactions) ->
          $scope.transactions = transactions

]

