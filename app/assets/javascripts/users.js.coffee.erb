<% url = Envelopes::Application.routes.url_helpers %>

$ ->
  $('#account input').selectOnFocus()

  $('#account .new').on 'click', (event) ->
    event.preventDefault()
    newIndex = $('#account ul.questions li').length - 1
    newItemHtml = $('#questions_template').text().replace /index/g, newIndex
    $(newItemHtml)
      .hide()
      .insertBefore($(this).parent())
      .slideDown()
      .find('input').selectOnFocus()
  
  $('#account').on 'click', '.destroy', (event) ->
    event.preventDefault()
    $(this).parent().slideUp ->
      $(this).remove()

  $('#account .import').click ->
    modal = showModal
      content: '#import_modal'
      width: 510
      className: 'import-modal'
    $.post("<%= url.import_transactions_path %>")
      .error (data) ->
        hideModal()
        regex = /<h1>\s*([^<]*)\s*<\/h1>\s*<pre>([^<]*)/m
        matches = regex.exec(data.responseText)
        
        $("<div class='flash alert'><div><pre style='margin:0'>" + matches[1] + "\n" + matches[2] + "</pre></div></div>")
          .appendTo('body')
          .delay(8000)
          .fadeOut ->
            $(this).remove()
