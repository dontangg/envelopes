# Place all the behaviors and hooks related to the matching controller here.
<% url = Envelopes::Application.routes.url_helpers %>

setCurrentDateRange = ->
  startDate = $('#date_range_picker .start').datepicker('getDate')
  endDate = $('#date_range_picker .end').datepicker('getDate')
  $('#date_range_picker .start').datepicker("option", "maxDate", endDate)
  $('#date_range_picker .end').datepicker("option", "minDate", startDate)
  
  startDateText = $.datepicker.formatDate 'M d, yy', startDate
  endDateText = $.datepicker.formatDate 'M d, yy', endDate
  $('#date_range_picker .current').text(startDateText + " - " + endDateText)

cancelDatePicker = (event) ->
  if $('#date_range_picker .popup').is(':visible')
    $('#date_range_picker .popup').hide()
    $('#date_range_picker .start').datepicker('setDate', $('#date_range_picker .start').data('initialDate'))
    $('#date_range_picker .end').datepicker('setDate', $('#date_range_picker .end').data('initialDate'))
    setCurrentDateRange()

@setupTransactionsList = ->
  $('#date_range_picker').click (event) ->
    event.stopPropagation()
    $(this).children('.popup').show()
  
  $('html').click cancelDatePicker
  $('#date_range_picker .actions a').click (event) ->
    event.preventDefault()
    event.stopPropagation()
    cancelDatePicker()
  
  $('#date_range_picker .start').datepicker
    onSelect: setCurrentDateRange
    dateFormat: 'yy-mm-dd'
    altField: '#start_date'
    defaultDate: $('#date_range_picker .start').data('initialDate')
  $('#date_range_picker .end').datepicker
    onSelect: setCurrentDateRange
    dateFormat: 'yy-mm-dd'
    altField: '#end_date'
    defaultDate: $('#date_range_picker .end').data('initialDate')
  setCurrentDateRange()

  $('select').selectToAutocomplete()
  
  $('#transactions_list input').selectOnFocus()
  
  $('#transactions_list .payee input').autocomplete
    source: "<%= url.suggest_payee_transactions_path %>"
    minLength: 2

$ ->
  $('#dashboard > ul').masonry
    itemSelector: '#dashboard > ul > li',
    columnWidth: 250,
    gutterWidth: 40,
    isAnimated: true
  
  if $('#transactions_list').length > 0
    setupTransactionsList()
  
  $(document).on('blur', '.amount input', ->
    $this = $(this)
    value = parseFloat $this.val().replace(/[^-.0-9]/g, "")
    if isNaN(value)
      $this.val ""
    else
      value = value.toFixed(2).replace /([.0-9]+)/g, "$$$1"
      $this.val value
  )
  
  autosave '#envelope_details', 'blur', '.payee input, .amount input', "<%= CGI.unescape url.transaction_path('{id}') %>"
  autosave '#envelope_details', 'change', 'select', "<%= CGI.unescape url.transaction_path('{id}') %>"
  
  $('#envelope_aside .transfer').click ->
    showModal
      content: '#transfer_modal'
      width: 510
      className: 'transfer-modal'
    
    modal = $('.transfer-modal')
    modal.find('select').selectToAutocomplete()
    modal.find('input[type=text], input:not([type])]').selectOnFocus()
    modal.find('a').click ->
      hideModal()
  