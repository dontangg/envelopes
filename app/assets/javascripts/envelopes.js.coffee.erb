# Place all the behaviors and hooks related to the matching controller here.
<% url = Envelopes::Application.routes.url_helpers %>

setCurrentDateRange = ->
  startDate = $('#date_range_picker .start').datepicker('getDate')
  endDate = $('#date_range_picker .end').datepicker('getDate')
  $('#date_range_picker .start').datepicker("option", "maxDate", endDate)
  $('#date_range_picker .end').datepicker("option", "minDate", startDate)
  
  startDateText = $.datepicker.formatDate 'M d, yy', startDate
  endDateText = $.datepicker.formatDate 'M d, yy', endDate
  $('#date_range_picker .current').text(startDateText + " - " + endDateText)

cancelDatePicker = (event) ->
  if $('#date_range_picker .popup').is(':visible')
    $('#date_range_picker .popup').hide()
    $('#date_range_picker .start').datepicker('setDate', $('#date_range_picker .start').data('initialDate'))
    $('#date_range_picker .end').datepicker('setDate', $('#date_range_picker .end').data('initialDate'))
    setCurrentDateRange()

@setupTransactionsList = ->
  $('#date_range_picker').click (event) ->
    event.stopPropagation()
    $(this).children('.popup').show()
  
  $('html').click cancelDatePicker
  $('#date_range_picker .actions a').click (event) ->
    event.preventDefault()
    event.stopPropagation()
    cancelDatePicker()
  
  $('#date_range_picker .start').datepicker
    onSelect: setCurrentDateRange
    dateFormat: 'yy-mm-dd'
    altField: '#start_date'
    defaultDate: $('#date_range_picker .start').data('initialDate')
  $('#date_range_picker .end').datepicker
    onSelect: setCurrentDateRange
    dateFormat: 'yy-mm-dd'
    altField: '#end_date'
    defaultDate: $('#date_range_picker .end').data('initialDate')
  setCurrentDateRange()

  $('select').selectToAutocomplete()
  
  $('#transactions_list input').selectOnFocus()
  
  $('#transactions_list .payee input').autocomplete
    source: "<%= CGI.unescape url.suggest_payee_transactions_path %>"
    minLength: 2

###
# On DOM Ready
###

$ ->
  $('#dashboard > ul').masonry
    itemSelector: '#dashboard > ul > li',
    columnWidth: 250,
    gutterWidth: 40,
    isAnimated: true
  
  ###
  # Show envelope
  ###

  if $('#transactions_list').length > 0
    setupTransactionsList()
  
  autosave '#envelope_details', 'blur', '.payee input, .amount input', "<%= CGI.unescape url.transaction_path('{id}') %>"
  autosave '#envelope_details', 'change', 'select', "<%= CGI.unescape url.transaction_path('{id}') %>"
  
  $('#envelope_details').on 'blur', '.amount input', ->
    $this = $(this)
    associated_transaction = $('#transaction_' + $this.data('associated-transaction-id'))
    if associated_transaction.length > 0
      newAmount = parseFloat($this.val().replace(/[^-.0-9]/g, "")) * -1
      associated_transaction.find('.amount input').val(newAmount.toFixed(2).replace /([.0-9]+)/g, "$$$1")
  
  $('#update_all_transactions')
    .on('ajax:beforeSend', ->
      cancelDatePicker()
      $('#update_all_transactions .loading-overlay').css('opacity', 0.7).show()
    ).on('ajax:error', (event, xhr, status, error) ->
      showAlert error
      $('#update_all_transactions .loading-overlay').hide()
    )
    
  
  $('#envelope_aside .transfer').click ->
    modal = showModal
      content: '#transfer_modal'
      width: 510
      className: 'transfer-modal'
    
    modal.find('form').on('ajax:beforeSend', ->
      modal.find('.loading').show()
    ).on('ajax:error', (event, xhr, status, error) ->
      showAlert error
      modal.find('.loading').hide()
    )
    
    modal.find('select').selectToAutocomplete()
    modal.find('input[type=text], input:not([type])]').selectOnFocus()
    modal.find('a').click ->
      hideModal()

  ###
  # Manage Envelopes
  ###

  $('#manage_envelopes input[type=text], input:not([type])]').selectOnFocus()
  $('#envelopes_aside .new').click ->
    modal = showModal
      content: '#new_envelope_modal'
      width: 520
      className: 'new-envelope-modal'
    
    modal.find('form').on('ajax:beforeSend', ->
      modal.find('.loading').show()
    ).on('ajax:success', (event, data, status, xhr) ->
      #modal.find('.loading').hide()
      hideModal()
    ).on('ajax:error', (event, xhr, status, error) ->
      try
        errors = $.parseJSON(xhr.responseText)
        errorMessage = "The envelope wasn't saved!<ul>"
        for errorField of errors
          errorMessage += "<li>" + errorField + " " + errors[errorField].join(' and ') + "</li>"
        errorMessage += "</ul>"
      catch ex
        errorMessage = xhr.responseText
      
      showAlert errorMessage
      modal.find('.loading').hide()
    )
    
    modal.find('select').selectToAutocomplete()
    modal.find('input[type=text], input:not([type])]').selectOnFocus()
    modal.find('a').on 'click', ->
      hideModal()

  autosave '#manage_envelopes', 'blur', 'input, select', "<%= CGI.unescape url.envelope_path('{id}') %>"

  $('td.expense-frequency div').popover
    placement: 'bottom',
    trigger: 'manual'
    title: 'Edit Frequency',
    content: "<input />"
  .click (event) ->
    event.stopPropagation()
    $('td.expense-frequency div').popover 'hide'
    $(this).popover 'toggle'
  if $('td.expense-frequency div').length > 0
    $(document).click ->
      $('td.expense-frequency div').popover 'hide'

  ###
  # Fill Envelopes
  ###

  $('#fill_envelopes input[type=text]')
    .selectOnFocus()
    .on 'keyup', ->
      $this = $(this)
      
      total = 0
      parent = $this.parent().closest('ul')
      parent.find('input').each (index, item) ->
        total += getAmount(item.value) || 0
      parent.prev().find('.sum').text(numberToCurrency(total))
      
      total = 0
      grandParent = parent.parent().closest('ul')
      grandParent.find('input').each (index, item) ->
        total += getAmount(item.value) || 0
      grandParent.prev().find('.sum').text(numberToCurrency(total))
      
      total = 0
      $('#fill_envelopes input[type=text]').each (index, item) ->
        total += getAmount(item.value) || 0
      totalAllocation = numberToCurrency(total * -1)
      totalAllocation = "-" + totalAllocation if total == 0
      totalAllocation = "+" + totalAllocation if total < 0
      $('#fill_aside .allocation').text(totalAllocation)
      available = getAmount $('#fill_aside .available').text()
      $('#fill_aside .remaining').text numberToCurrency(available - total)
      
  
  $('#fill_envelopes .expand, #fill_envelopes .collapse').click (event) ->
    event.preventDefault()
    $this = $(this)
    childEnvelopes = $this.closest('li').children('ul')
    childEnvelopes.slideToggle ->
      if childEnvelopes.is ':visible'
        $this.removeClass('expand').addClass('collapse')
      else
        $this.removeClass('collapse').addClass('expand')
  
